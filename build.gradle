group = 'com.dmdirc'

apply plugin: 'com.dmdirc.git-version'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'idea'

idea {
    module {
         sourceDirs = [file('src'), file('generated')]
         testSourceDirs = [file('test'), file('generated_tests')]
         generatedSourceDirs = [file('generated'), file('generated_tests')]
    }
}

configurations {
    bundle

    compile {
        extendsFrom bundle
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 2, 'minutes'
    resolutionStrategy.cacheChangingModulesFor 2, 'minutes'
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
    compile group: 'com.squareup.dagger', name: 'dagger-compiler', version: '1.2.1'
    compile group: 'com.google.auto.value', name: 'auto-value', version: '1.0'

    bundle group: 'com.squareup.dagger', name: 'dagger', version: '1.2.1'
    bundle group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.08'
    bundle group: 'com.brsanthu', name: 'migbase64', version: '2.2'
    bundle group: 'net.kencochrane.raven', name: 'raven', version: '5.0.1'
    bundle group: 'com.google.guava', name:'guava', version: '18.0'
    bundle group: 'net.engio', name: 'mbassador', version: '1.2.0'
    bundle group: 'com.dmdirc', name: 'util', version: '+', changing: true
    bundle group: 'com.dmdirc.parser', name: 'common', version: '+', changing: true

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.17'
    testCompile group: 'com.google.jimfs', name: 'jimfs', version: '1.0'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['res']
    }
    test {
        java.srcDirs = ['test']
        resources.srcDirs = ['test-res']
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'http://artifactory.dmdirc.com/artifactory/repo'
    }
}

apply from: 'analysis.gradle'
apply from: 'publishing.gradle'
apply from: 'jar.gradle'
apply from: 'fatjar.gradle'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://artifactory.dmdirc.com/artifactory/repo' }
        maven { url 'https://dl.bintray.com/dmdirc/releases/' }
    }

    dependencies {
        classpath group: 'com.dmdirc', name: 'git-version', version: '1.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
